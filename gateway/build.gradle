plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'pyc.lopatuxin'
version = '0.0.1-SNAPSHOT'
description = 'gateway'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    configureEach {
        resolutionStrategy {
            // CVE-2026-1225: Malicious logback.xml allows instantiation of arbitrary classes
            // GHSA-qqpg-mvqg-649v | CVSS 5.0 — force patched version above 1.5.22
            force 'ch.qos.logback:logback-core:1.5.32'
            force 'ch.qos.logback:logback-classic:1.5.32'
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/release' }
}

dependencies {
    implementation platform('org.springframework.cloud:spring-cloud-dependencies:2025.1.1')
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway-server-webflux'

    // CVE-2026-1225: исключаем уязвимый logback, явно подтягиваем patched версию
    implementation('org.springframework.boot:spring-boot-starter-logging') {
        exclude group: 'ch.qos.logback', module: 'logback-core'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    implementation 'ch.qos.logback:logback-classic:1.5.32'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:3.0.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'sun.stdout.encoding', 'UTF-8'
    systemProperty 'sun.stderr.encoding', 'UTF-8'

    jvmArgs '-Dfile.encoding=UTF-8', '-Dconsole.encoding=UTF-8'
}
